CREATE OR ALTER PROCEDURE dbo.usp_Alterna_GetDuplicateCustomers
(
  @MaxCandidatesPerNew int = 500,
  @StatusCode NVARCHAR(250) = N'PENDING',
  @DifferenceThreshold tinyint = 1  -- 1 -- Can have a value between 1 and 4 the higher, the stricter
)
AS
BEGIN
  SET NOCOUNT ON;

  ;WITH NC AS
  (
    SELECT
		T24C.Id AS NewCustomerId,
		dbo.Fn_NormalizeWord(T24C.DateOfBirth) AS DateOfBirth,
		dbo.Fn_NormalizeWord(T24C.GivenNames) AS GivenNames,
		dbo.Fn_NormalizeWord(T24C.FamilyName) AS FamilyName,
		dbo.Fn_NormalizeWord(T24C.FatherName) AS FatherName,
		dbo.Fn_NormalizeWord(T24C.MoFirstName) AS MoFirstName,
		dbo.Fn_NormalizeWord(T24C.BirthRegister) AS BirthRegister,
		dbo.Fn_NormalizeWord(T24C.BirthRegNo) AS BirthRegNo,
		dbo.Fn_NormalizeWord(T24C.Gender) AS Gender
    FROM dbo.t_NewT24Customers AS NT24C
		JOIN dbo.t_T24Customer AS T24C ON NT24C.CustomerId = T24C.Id
    WHERE NT24C.StatusCode = @StatusCode
  )
  SELECT
    nc.NewCustomerId,
    c.CustomerId,

    -- New customer values
    nc.DateOfBirth   AS New_DateOfBirth,
    nc.GivenNames    AS New_GivenNames,
    nc.FamilyName    AS New_FamilyName,
    nc.FatherName    AS New_FatherName,
    nc.MoFirstName   AS New_MoFirstName,
    nc.BirthRegister AS New_BirthRegister,
    nc.BirthRegNo    AS New_BirthRegNo,
    nc.Gender        AS New_Gender,

    -- Candidate values
    c.Db_DateOfBirth,
    c.Db_GivenNames,
    c.Db_FamilyName,
    c.Db_FatherName,
    c.Db_MoFirstName,
    c.Db_BirthRegister,
    c.Db_BirthRegNo,
    c.Db_Gender,

    -- Exact flags
    c.Match_DateOfBirth,
    c.Match_BirthRegNo,
    c.Match_BirthRegister,
    c.Match_Gender,

    -- Fuzzy flags
    c.Match_GivenNames,
    c.Match_FamilyName,
    c.Match_FatherName,
    c.Match_MoFirstName
  FROM NC AS nc
CROSS APPLY
(
    SELECT TOP (@MaxCandidatesPerNew)
        t.Id AS CustomerId,

        dbo.Fn_NormalizeWord(t.DateOfBirth)   AS Db_DateOfBirth,
        dbo.Fn_NormalizeWord(t.GivenNames)    AS Db_GivenNames,
        dbo.Fn_NormalizeWord(t.FamilyName)    AS Db_FamilyName,
        dbo.Fn_NormalizeWord(t.FatherName)    AS Db_FatherName,
        dbo.Fn_NormalizeWord(t.MoFirstName)   AS Db_MoFirstName,
        dbo.Fn_NormalizeWord(t.BirthRegister) AS Db_BirthRegister,
        dbo.Fn_NormalizeWord(t.BirthRegNo)    AS Db_BirthRegNo,
        dbo.Fn_NormalizeWord(t.Gender)        AS Db_Gender,

        -- Exact match flags
        CAST(CASE WHEN dbo.Fn_NormalizeWord(t.DateOfBirth)   = nc.DateOfBirth   THEN 1 ELSE 0 END AS bit) AS Match_DateOfBirth,
        CAST(CASE WHEN dbo.Fn_NormalizeWord(t.BirthRegNo)    = nc.BirthRegNo    THEN 1 ELSE 0 END AS bit) AS Match_BirthRegNo,
        CAST(CASE WHEN dbo.Fn_NormalizeWord(t.BirthRegister) = nc.BirthRegister THEN 1 ELSE 0 END AS bit) AS Match_BirthRegister,
        CAST(CASE WHEN dbo.Fn_NormalizeWord(t.Gender)        = nc.Gender        THEN 1 ELSE 0 END AS bit) AS Match_Gender,

        -- Fuzzy flags
        CAST(CASE WHEN t.GivenNames  IS NOT NULL
                  AND DIFFERENCE(nc.GivenNames, dbo.Fn_NormalizeWord(t.GivenNames))   >= @DifferenceThreshold THEN 1 ELSE 0 END AS bit) AS Match_GivenNames,
        CAST(CASE WHEN t.FamilyName IS NOT NULL
                  AND DIFFERENCE(nc.FamilyName, dbo.Fn_NormalizeWord(t.FamilyName))   >= @DifferenceThreshold THEN 1 ELSE 0 END AS bit) AS Match_FamilyName,
        CAST(CASE WHEN t.FatherName IS NOT NULL
                  AND DIFFERENCE(nc.FatherName, dbo.Fn_NormalizeWord(t.FatherName))   >= @DifferenceThreshold THEN 1 ELSE 0 END AS bit) AS Match_FatherName,
        CAST(CASE WHEN t.MoFirstName IS NOT NULL
                  AND DIFFERENCE(nc.MoFirstName, dbo.Fn_NormalizeWord(t.MoFirstName)) >= @DifferenceThreshold THEN 1 ELSE 0 END AS bit) AS Match_MoFirstName

    FROM dbo.t_T24Customer AS t
    WHERE
        t.Id <> nc.NewCustomerId

        AND t.GivenNames  IS NOT NULL
        AND t.FamilyName  IS NOT NULL
        AND t.FatherName  IS NOT NULL
        AND t.MoFirstName IS NOT NULL

        AND DIFFERENCE(nc.GivenNames,  dbo.Fn_NormalizeWord(t.GivenNames))  >= @DifferenceThreshold
        AND DIFFERENCE(nc.FamilyName,  dbo.Fn_NormalizeWord(t.FamilyName))  >= @DifferenceThreshold
        AND DIFFERENCE(nc.FatherName,  dbo.Fn_NormalizeWord(t.FatherName))  >= @DifferenceThreshold
        AND DIFFERENCE(nc.MoFirstName, dbo.Fn_NormalizeWord(t.MoFirstName)) >= @DifferenceThreshold

    ORDER BY t.Id
) AS c
  ORDER BY nc.NewCustomerId, c.CustomerId;
END
GO
